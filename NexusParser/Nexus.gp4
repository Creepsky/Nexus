grammar Nexus;

/* Parser */

file_declaration :   class_declaration
                   | extension_function ;

file : file_declaration* ;

class_declaration : CLASS name=IDENTIFIER template_list_declaration? LEFT_CURLY_BRACE class_member* RIGHT_CURLY_BRACE ;

class_member :    CPP_BLOCK
                | variable_declaration
                | include COLON
                ;

include :         INCLUDE LESS path=.*? GREATER #farInclude
                | INCLUDE APOSTROPHE path=.*? APOSTROPHE #nearInclude
                ;

type :    IDENTIFIER ARRAY_DECLARATION* #namedType
        | LEFT_BRACE type (COMMA type)*  RIGHT_BRACE ARRAY_DECLARATION* #tupleType
        | LEFT_BRACKET key=type ARROW_RIGHT value=type RIGHT_BRACKET ARRAY_DECLARATION* #mapType
	;

variable_declaration : type IDENTIFIER template_list_usage? (EQUAL expression)? COLON ;

function_parameter : type IDENTIFIER (EQUAL expression)?;

function_declaration :  type IDENTIFIER template_list_declaration?
                        LEFT_BRACE (function_parameter (COMMA function_parameter)*)? RIGHT_BRACE
                        LEFT_CURLY_BRACE function_body RIGHT_CURLY_BRACE ;

assignment_statement : left=expression EQUAL right=expression COLON ;

return_statement : RETURN expression COLON ;

variable_statement : type IDENTIFIER (EQUAL expression)? COLON ;

tuple_explosion_statement : IDENTIFIER IDENTIFIER (COMMA IDENTIFIER)* EQUAL expression COLON ;

if_statement :  IF LEFT_BRACE comparison RIGHT_BRACE
                LEFT_CURLY_BRACE then=function_body RIGHT_CURLY_BRACE
                ELSE LEFT_CURLY_BRACE otherwise=function_body RIGHT_CURLY_BRACE ;

while_statement : WHILE LEFT_BRACE comparison RIGHT_BRACE
                  LEFT_CURLY_BRACE function_body RIGHT_CURLY_BRACE ;

for_init :        assignment_statement
                | variable_statement ;

for_statement : FOR LEFT_BRACE for_init comparison COLON expression RIGHT_BRACE
                LEFT_CURLY_BRACE function_body RIGHT_CURLY_BRACE ;

member_access : element=IDENTIFIER DOT member=IDENTIFIER ;

function_call : IDENTIFIER LEFT_BRACE (expression (COMMA expression)*)? RIGHT_BRACE ;

extension_function_call : IDENTIFIER DOT function_call ;

function_body : function_body_statement* ;

function_body_statement :
                  CPP_BLOCK
                | assignment_statement
                | return_statement
                | function_declaration
                | variable_statement
                | if_statement
                | while_statement
                | for_statement
                | function_call COLON
                | extension_function_call COLON
                | tuple_explosion_statement
                | include COLON
                ;

key_value_pair : expression ARROW_RIGHT expression ;

expression :	  expression STAR expression #mul
                | expression SLASH expression #div
                | expression PLUS expression #add
                | expression MINUS expression #sub
                | LEFT_BRACE expression (COMMA expression)+ RIGHT_BRACE #tuple
                | LEFT_BRACE expression RIGHT_BRACE #paren
                | LEFT_BRACKET expression(COMMA expression)* RIGHT_BRACKET #array
                | LEFT_BRACKET key_value_pair (COMMA key_value_pair)* RIGHT_BRACKET #map
                | expression RANGE expression #range
                | expression EQUAL expression #test
                | expression LEFT_BRACKET expression RIGHT_BRACKET #arrayAccess
                | function_call #functionCall
                | extension_function_call #extensionFunctionCall
                | CPP_BLOCK #cppBlock
                | factor #fact ;

truth_value : TRUE | FALSE ;

number :  INTEGER INTEGER_SUFFIX?
        | REAL REAL_SUFFIX? ;

quoted_text : APOSTROPHE text=.*? APOSTROPHE ;

factor :      number
            | truth_value
            | BINARY
            | HEX
            | quoted_text
            | IDENTIFIER
            | member_access
            ;

comparison :      expression EQUAL expression
                | expression LESS expression
                | expression LESS_EQUAL expression
                | expression GREATER expression
                | expression GREATER_EQUAL expression ;

extension_type :  IDENTIFIER
                | type
                ;

extension_function :    returnType=type extensionType=type extensionTypeTemplates=template_list_declaration?
                        DOT (name=IDENTIFIER | OPERATOR operator)
                        functionTemplates=template_list_declaration?
                        LEFT_BRACE (function_parameter (COMMA function_parameter)*)? RIGHT_BRACE
                        LEFT_CURLY_BRACE function_body RIGHT_CURLY_BRACE ;

operator :   PLUS
           | MINUS
           | STAR
           | SLASH
           ;

template_list_declaration : LESS (template_list | variadic_template) GREATER ;

template_list_usage : LESS template_list GREATER ;

template_list : IDENTIFIER (COMMA IDENTIFIER)* ;

variadic_template : IDENTIFIER DOT_DOT_DOT  ;

/* Lexer */

COMMENT : '//' ~('\r'|'\n')* -> skip ;
MULTI_LINE_COMMENT : '/*' .*? '*/' -> skip ;
CPP_BLOCK : 'c++' WHITESPACE* LEFT_CURLY_BRACE PIPE WHITESPACE* .*? WHITESPACE* PIPE RIGHT_CURLY_BRACE ;
CLASS : 'class' ;
DOT_DOT_DOT : '...' ;
LEFT_CURLY_BRACE : '{' ;
RIGHT_CURLY_BRACE : '}' ;
LEFT_BRACE : '(' ;
RIGHT_BRACE : ')' ;
ARRAY_DECLARATION : '[]' ;
LEFT_BRACKET : '[' ;
RIGHT_BRACKET : ']' ;
COLON : ';' ;
ARROW_RIGHT : '->' ;
BINARY : ('b'|'B') 'x' ('0'|'1')+ ;
HEX : '0x' [0-9A-F]+ ;
INTEGER : ('-'|'+')?[0-9]+(','[0-9]+)* ;
REAL : ('-'|'+')?[0-9]+'.'[0-9]* ;
IF : 'if' ;
ELSE : 'else' ;
ELSE_IF : ELSE IF ;
WHILE : 'while' ;
FOR : 'for' ;
INCLUDE : 'include' ;
APOSTROPHE : '"' ;
EQUAL : '=' ;
LESS : '<' ;
LESS_EQUAL : '<=' ;
GREATER : '>' ;
GREATER_EQUAL : '>=' ;
TRUE : 'true' ;
FALSE : 'false' ;
PLUS : '+' ;
MINUS : '-' ;
STAR: '*' ;
SLASH: '/' ;
COMMA : ',' ;
PIPE : '|' ;
RETURN : 'return' ;
OPERATOR : 'operator' ;
RANGE : '..' ;
DOT : '.' ;
CPP : 'c++' ;
INTEGER_SUFFIX : '_' ('i'|'u') ('8'|'16'|'32'|'64') ;
REAL_SUFFIX : '_f' ('32'|'64') | 'f' | 'd' ;
IDENTIFIER : [a-zA-Z_-][a-zA-Z0-9_-]* ;
WHITESPACE : [ \r\t\n]+ -> skip ;
